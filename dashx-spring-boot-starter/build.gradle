import com.vanniktech.maven.publish.SonatypeHost

plugins {
    id 'java-library'
    id 'signing'

    alias(libs.plugins.gradle.maven.publish)
}

def version = libs.versions.dashx.get()
def group = libs.versions.group.get()
def artifactId = "dashx-spring-boot-starter"

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    api project(':dashx')

    implementation 'org.springframework.boot:spring-boot-autoconfigure:3.2.3'
    implementation 'org.springframework.boot:spring-boot-starter:3.2.3'
}

tasks.named('test') {
    useJUnitPlatform()
}

signing {
    useGpgCmd()
    sign(publishing.publications)
}

mavenPublishing {
    coordinates(group, artifactId, version)

    publishToMavenCentral(SonatypeHost.CENTRAL_PORTAL,true)

    pom {
        name = "DashX Spring Boot Starter"
        description = "DashX Starter for Spring Boot Applications"
        url = "https://github.com/dashxhq/dashx-java"

        licenses {
            license {
                name = "MIT License"
                url = "https://github.com/dashxhq/dashx-java/blob/main/LICENSE"
            }
        }

        developers {
            developer {
                id = "gulshan.dashx"
                name = "Gulshan Dhingra"
            }
        }

        scm {
            connection = "scm:git:github.com/dashxhq/dashx-java.git"
            developerConnection = "scm:git:ssh://github.com/dashxhq/dashx-java.git"
            url = "https://github.com/dashxhq/dashx-java"
        }
    }
}
